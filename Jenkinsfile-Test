pipeline {
  agent any
  environment {
    DOCKER_CREDENTIALS_ID = 'docker-hub-cred'
    REPO_URL = 'https://github.com/MyTruong28022004/spring-petclinic-microservices-fork.git'
    HELM_REPO_URL = 'https://github.com/thmthu/CD-for-pet-clinic.git'
    HELM_REPO_DIR = 'CD-for-pet-clinic'
    GIT_CREDENTIALS_ID = 'thmthu-git'
  }

  parameters {
    string(name: 'BRANCH_NAME', defaultValue: 'main', description: 'Branch to build')
  }

  stages {
    stage('Checkout') {
      steps {
        git branch: "${params.BRANCH_NAME}", url: "${REPO_URL}"
      }
    }

    stage('Detect Changed Services') {
      steps {
        script {
          def prevCommit = sh(script: 'git rev-parse HEAD^ || git rev-parse HEAD', returnStdout: true).trim()
          def changedFiles = sh(script: "git diff --name-only ${prevCommit} HEAD", returnStdout: true).trim().split("\n")

          def services = [
            'spring-petclinic-customers-service',
            'spring-petclinic-vets-service',
            'spring-petclinic-visits-service',
            'spring-petclinic-genai-service',
            'spring-petclinic-admin-server',
            'spring-petclinic-api-gateway',
            'spring-petclinic-config-server',
            'spring-petclinic-discovery-server'
          ]

          def changedServices = services.findAll { service ->
            changedFiles.any { it.startsWith("${service}/") }
          }

          echo "Changed services: ${changedServices}"
          env.CHANGED_SERVICES = changedServices.join(',')
        }
      }
    }

    stage('Determine Image Tag') {
      steps {
        script {
          def tag = sh(script: "git describe --tags --exact-match HEAD || echo ''", returnStdout: true).trim()
          def commitId = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
          env.IMAGE_TAG = tag ? tag : commitId
          echo "Using IMAGE_TAG=${env.IMAGE_TAG}"
          env.HAS_TAG = tag ? "true" : "false"
        }
      }
    }

    stage('Skip Build (No Services Changed)') {
      when {
        not {
          expression { return env.CHANGED_SERVICES?.trim() }
        }
      }
      steps {
        echo "No changed services detected. Skipping build and push."
      }
    }

    stage('Build & Push Changed Images') {
      when {
        expression { return env.CHANGED_SERVICES?.trim() }
      }
      steps {
        script {
          def changedServices = env.CHANGED_SERVICES.split(',').findAll { it?.trim() }

          def imageMap = [
            'spring-petclinic-customers-service': 'mytruong28022004/spring-petclinic-customers-service',
            'spring-petclinic-vets-service'    : 'mytruong28022004/spring-petclinic-vets-service',
            'spring-petclinic-visits-service'   : 'mytruong28022004/spring-petclinic-visits-service',
            'spring-petclinic-genai-service'   : 'mytruong28022004/spring-petclinic-genai-service',
            'spring-petclinic-admin-server':  'mytruong28022004/spring-petclinic-admin-server',
            'spring-petclinic-api-gateway':  'mytruong28022004/spring-petclinic-api-gateway',
            'spring-petclinic-config-server':  'mytruong28022004/spring-petclinic-config-server',
            'spring-petclinic-discovery-server':  'mytruong28022004/spring-petclinic-discovery-server'
          ]

          changedServices.each { service ->
            def imageName = imageMap[service]
            echo "Building and pushing image for: ${service} as ${imageName}:${env.IMAGE_TAG}"

            dir(service) {
              sh "../mvnw clean install -P buildDocker -DskipTests"

              withCredentials([usernamePassword(credentialsId: env.DOCKER_CREDENTIALS_ID, passwordVariable: 'DOCKER_PASSWORD', usernameVariable: 'DOCKER_USERNAME')]) {
                sh 'echo $DOCKER_PASSWORD | docker login --username $DOCKER_USERNAME --password-stdin'
                sh "docker tag ${imageName} ${imageName}:${env.IMAGE_TAG}"
                sh "docker push ${imageName}:${env.IMAGE_TAG}"
                sh "docker push ${imageName}:latest"
              }
            }
          }
        }
      }
    }

    stage('Clone Helm Repo and Overwrite Tag') {
      when {
        expression { return env.CHANGED_SERVICES?.trim() }
      }
      steps {
        dir('cd-temp') {
          git url: "${HELM_REPO_URL}", credentialsId: "${GIT_CREDENTIALS_ID}", branch: "master"
    
          script {
            def valuesFile = env.HAS_TAG == 'true' ? 'spring-pet-clinic/values_staging.yaml' : 'spring-pet-clinic/values_dev.yaml'
            def changedServices = env.CHANGED_SERVICES.split(',').findAll { it?.trim() }
    
            // Map tên service -> key trong file values.yaml
            def helmKeyMap = [
              'spring-petclinic-config-server'     : 'config',
              'spring-petclinic-discovery-server'  : 'discovery',
              'spring-petclinic-api-gateway'       : 'gateway',
              'spring-petclinic-customers-service' : 'customers',
              'spring-petclinic-visits-service'    : 'visits',
              'spring-petclinic-vets-service'      : 'vets',
              'spring-petclinic-admin-server'      : 'admin'
            ]
    
            // In thư mục để debug
            sh "ls -la /var/lib/jenkins/workspace/${env.JOB_NAME}/cd-temp"
    
            changedServices.each { service ->
              def yamlKey = helmKeyMap[service]
              if (!yamlKey) {
                error "Không tìm thấy mapping cho service: ${service}"
              }
    
              echo "Updating tag for ${yamlKey} -> ${env.IMAGE_TAG}"
    
              // Cập nhật tag trong values file
              sh """
                docker run --rm -u 0 -v \$(pwd):/workdir mikefarah/yq \
                e '.${yamlKey}.image.tag = "${env.IMAGE_TAG}"' -i /workdir/${valuesFile}
              """
            }
    
            // Commit và push nếu có thay đổi
            sh "git config --global user.email 'thmt.thu@gmail.com'"
            sh "git config --global user.name 'thmthu'"
            sh "git add ${valuesFile}"
    
            // Chỉ commit nếu có thay đổi thực sự
            def changes = sh(script: "git diff --cached --quiet || echo 'has-changes'", returnStdout: true).trim()
            if (changes == 'has-changes') {
              sh "git commit -m 'Update image tags to ${env.IMAGE_TAG} for services: ${env.CHANGED_SERVICES}'"
              withCredentials([usernamePassword(credentialsId: "${GIT_CREDENTIALS_ID}", passwordVariable: 'GIT_PASSWORD', usernameVariable: 'GIT_USERNAME')]) {
                sh 'git remote set-url origin https://${GIT_USERNAME}:${GIT_PASSWORD}@github.com/thmthu/CD-for-pet-clinic.git'
                sh 'git push origin master'
              }
            } else {
              echo "Không có thay đổi trong file Helm values, bỏ qua commit."
            }
          }
        }
      }
    }
  }

  post {
    always {
      echo "Cleaning up Docker images..."
      sh 'docker system prune -f || true'
    }
  }
}
